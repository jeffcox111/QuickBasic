DECLARE SUB REGISTRATION ()
DECLARE SUB INTRO ()
DECLARE SUB TEST ()
DECLARE SUB LEVELSUB ()
DECLARE SUB ALARM ()
DECLARE SUB INSTRUCTIONS ()
DECLARE SUB WARPSUB ()
DECLARE SUB SREPAIRS ()
DECLARE SUB KREPAIRS ()
DECLARE SUB SHIPDESTROYED ()
DECLARE SUB IMPULSESUB ()
DECLARE SUB SHIPSSUB ()
DECLARE SUB PHASERSSUB ()
DECLARE SUB ENERGYSUB ()
DECLARE SUB BORDER ()
DECLARE SUB DRIVESUB ()
DECLARE SUB COMMANDSSUB ()
DECLARE SUB MAPSETUP ()
DECLARE SUB DISPLAY ()
DECLARE SUB SHEILDSSUB ()
DECLARE SUB OMEGANSSUB ()
DECLARE SUB TORPEDOESSUB ()
DECLARE SUB KDESTROYED ()
DECLARE SUB STARSSUB ()
DECLARE SUB ENEMYFIRESUB ()
DECLARE SUB DOCKING ()
DECLARE SUB WINGAME ()
DECLARE SUB KLMOVE ()
DECLARE SUB DAMAGESUB ()
DECLARE SUB RAND ()
COMMON SHARED OMEGANS, SHEILDS, PHASERS, TORPEDOES, DAMAGE, ENERGY
COMMON SHARED STARBASES, DRIVE, SHLDS$, SECTORX, SECTORY, TOTAL
COMMON SHARED IMPULSE, KHIT, SCORE, SBX, SBY, SHEILDSC, NEXTTRN
COMMON SHARED SYSDMG, PDMG, WDMG, SDMG, IDMG, TDMG, STRDT, KSTART
COMMON SHARED RNDN, DATEOLD, DATENEW, DATE
COMMON SHARED COMMANDS$, SYSTEM$, LEVEL, KDSTRYD, LSTRDT
RANDOMIZE TIMER
CLS
SCREEN 13
WIDTH 40, 25
CALL INTRO
CLS
COLOR 13
PRINT "CAPSLOCK"; : COLOR 5: PRINT " MUST BE ON TO OPERATE THIS GAME"
PRINT : COLOR 5
PRINT "INSTRUCTIONS? ("; : COLOR 13
PRINT "Y"; : COLOR 5: PRINT "ES OR "; : COLOR 13: PRINT "N"; : COLOR 5: PRINT "O)"
DO
A$ = INKEY$
LOOP UNTIL A$ = "Y" OR A$ = "N"
IF A$ = "Y" THEN CALL INSTRUCTIONS
CLS


'VARIABLES

DIM SHARED SECTOR(10, 10), TORPEDO(2), PHASERS(3)
DIM SHARED SBX(4), SBY(4), STX(100), STY(100), STC(100), DAMAGE(3)
DIM SHARED BRDR(25, 40), KSHLD(40), KTYPE(40), KX(3), KY(3), KCLR(40)
DIM SHARED KN(3), KSX(40), KSY(40), KR(3), RANGE$(3), KC(3), KDMG(40)
DIM SHARED RNDM(3), RN(3), KMOVE(3), EVADING(3), LEAVING(3)
ENERGY = 5000: SHEILDS = 1000: TORPEDOES = 10: PHASERS = 1000: DAMAGE = 0
DRIVE = 1000: SHLDS$ = "N": IMPULSE = 500
CALL DAMAGESUB: CALL LEVELSUB
CLS

SYSTEM$ = "A"
KSTART = OMEGANS
DATENEW = STRDT
DATA S,M,L
FOR A = 1 TO 3: READ RANGE$(A): NEXT

'GAMEPLAY

CALL MAPSETUP
CALL STARSSUB
CALL BORDER
CALL DISPLAY
CALL SHEILDSSUB
FOR A = 1 TO OMEGANS
 TOTAL = TOTAL + KTYPE(A) * 250
NEXT A

RESTART:
J0 = SECTOR(SECTORX, SECTORY)
IF NEXTTRN = 2 AND J0 > 0 AND J0 < 5 THEN NEXTTRN = 1
IF NEXTTRN = 1 THEN
 STRDT = STRDT - 1
 CALL ENERGYSUB
 CALL DISPLAY
END IF
NEXTTRN = 0
CALL COMMANDSSUB
CALL DISPLAY
J0 = SECTOR(SECTORX, SECTORY)
IF J0 > 0 AND J0 < 5 AND NEXTTRN > 0 THEN CALL ENEMYFIRESUB
FOR A = 1 TO DATE
 IF NEXTTRN > 0 THEN
  CALL KREPAIRS: CALL SREPAIRS: CALL KLMOVE
 END IF
NEXT A
IF NEXTTRN > 0 THEN CALL SHIPSSUB: CALL STARSSUB
IF SECTOR(SECTORX, SECTORY) >= 9 THEN CALL DOCKING
IF STRDT <= 0 THEN CALL WINGAME
IF KDSTRYD = OMEGANS THEN CALL WINGAME
GOTO RESTART

SUB ALARM
 FOR QQ = 1 TO 3
 SA = 1000: SB = 1100
 FOR SND = 1 TO 50
 FOR SS = SA TO SB STEP 20
 SOUND SS, .05
 NEXT SS
 SA = SA + 2: SB = SB + 2
 NEXT SND
 NEXT QQ
END SUB

SUB BORDER

FOR A = 1 TO 40
BRDR(1, A) = 196
BRDR(11, A) = 196
BRDR(23, A) = 196
IF A < 24 THEN
 BRDR(A, 1) = 179
 BRDR(A, 40) = 179
END IF
IF A < 12 THEN BRDR(11 + A, 24) = 179
NEXT A
BRDR(1, 1) = 218
BRDR(1, 40) = 191
BRDR(11, 1) = 195
BRDR(11, 24) = 194
BRDR(11, 40) = 180
BRDR(23, 1) = 192
BRDR(23, 24) = 193
BRDR(23, 40) = 217

END SUB

SUB COMMANDSSUB
NEWCOMMAND:
LOCATE 12, 2: COLOR 7: PRINT "COMMAND: ": LOCATE 12, 11: COLOR 15
PRINT SPACE$(11)
A$ = ""
DO
 A$ = INKEY$
LOOP WHILE A$ <> "S" AND A$ <> "P" AND A$ <> "W" AND A$ <> "T" AND A$ <> "I" AND A$ <> CHR$(9) AND A$ <> "E"
LOCATE 12, 11
NO = 0
IF A$ = "S" AND SHEILDS <= 0 THEN NO = 1
IF A$ = "P" AND PHASERS <= 0 THEN NO = 1
IF A$ = "W" AND DRIVE <= 0 THEN NO = 1
IF A$ = "T" AND TORPEDOES <= 0 THEN NO = 2
IF A$ = "I" AND IMPULSE <= 0 THEN NO = 1
ZZ = SECTOR(SECTORX, SECTORY)
IF A$ = "P" AND ZZ = 0 THEN NO$ = "NO OMEGANS!": NO = 3
IF A$ = "I" AND ZZ = 0 THEN NO$ = "NO OMEGANS!": NO = 3
IF A$ = "T" AND ZZ = 0 THEN NO$ = "NO OMEGANS!": NO = 3
IF NO = 1 THEN NO$ = "NO ENERGY!"
IF NO = 2 THEN NO$ = "NO TORPEDOES!"
IF NO > 0 THEN
 COLOR 15
 LOCATE 12, 2: PRINT SPACE$(22)
 LOCATE 12, 2: PRINT NO$
FOR TM = 1 TO 6000: NEXT
 GOTO NEWCOMMAND
END IF
COMMANDS$ = A$
IF COMMANDS$ = CHR$(9) THEN
 PRINT "WAITING"
FOR TM = 1 TO 4000: NEXT
 NEXTTRN = 1
ELSEIF COMMANDS$ = "S" THEN
 PRINT "SHEILDS"
FOR TM = 1 TO 4000: NEXT
 SHEILDSC = 1: CALL SHEILDSSUB
ELSEIF COMMANDS$ = "T" THEN
 PRINT "TORPEDOES"
FOR TM = 1 TO 4000: NEXT
 CALL TORPEDOESSUB
ELSEIF COMMANDS$ = "P" THEN
 PRINT "PHASERS"
FOR TM = 1 TO 4000: NEXT
 CALL PHASERSSUB
ELSEIF COMMANDS$ = "W" THEN
 PRINT "WARP DRIVE"
FOR TM = 1 TO 4000: NEXT
 CALL DRIVESUB
ELSEIF COMMANDS$ = "I" THEN
 PRINT "IMPULSE"
FOR TM = 1 TO 4000: NEXT
 CALL IMPULSESUB
ELSEIF COMMANDS$ = "E" THEN
 PRINT "ENERGY"
FOR TM = 1 TO 4000: NEXT
 CALL ENERGYSUB
END IF
ENDCOMMANDS:
END SUB

SUB DAMAGESUB

J99 = INT((DAMAGE - 10) / 15)
IF J99 < 0 THEN J99 = 0
IF SYSDMG >= J99 THEN GOTO ENDDAMAGE
DO
NEWDMG:
 NEW = 0: TT = 0
 A = INT(RND(1) * 5) + 1
 IF A = 1 AND PDMG = 1 THEN NEW = 1
 IF A = 2 AND TDMG = 1 THEN NEW = 1
 IF A = 3 AND SDMG = 1 THEN NEW = 1
 IF A = 4 AND WDMG = 1 THEN NEW = 1
 IF A = 5 AND IDMG = 1 THEN NEW = 1
 IF NEW = 1 THEN GOTO NEWDMG
 TT = 1
 IF A = 1 THEN PDMG = 1: A$ = "PHASERS"
 IF A = 2 THEN TDMG = 1: A$ = "TORPEDOES"
 IF A = 3 THEN SDMG = 1: A$ = "SHEILDS"
 IF A = 4 THEN WDMG = 1: A$ = "WARP DRIVE"
 IF A = 5 THEN IDMG = 1: A$ = "IMPULSE"
 SYSDMG = SYSDMG + 1
 LOCATE 12, 2: PRINT SPACE$(22)
 COLOR 15
 LOCATE 12, 2: PRINT A$; " DAMAGED!"
FOR TM = 1 TO 6000: NEXT
J99 = INT((DAMAGE - 10) / 15)
IF J99 < 0 THEN J99 = 0
LOOP WHILE SYSDMG < J99
ENDDAMAGE:
END SUB

SUB DISPLAY
STATUS:
'STATUS

A = 3: B = 15: C = 14: D = 11

'--SHEILDS--
IF SDMG = 1 THEN A = 4: D = 14 ELSE A = 7: D = 15
LOCATE C, 2: COLOR D: PRINT "S"; : COLOR A: PRINT "HEILDS:";
PRINT SPACE$(12): LOCATE C, 10
B = 2
IF SHEILDS <= 500 THEN B = 14
IF SHEILDS <= 250 THEN B = 4
COLOR B: PRINT STR$(SHEILDS); : COLOR A: C = C + 1
IF SYSTEM$ = "S" OR SYSTEM$ = "A" THEN COLOR 9: PRINT " *"

'--PHASERS--
IF PDMG = 1 THEN A = 4: D = 14 ELSE A = 7: D = 15
LOCATE C, 2: COLOR D: PRINT "P"; : COLOR A: PRINT "HASERS:";
PRINT SPACE$(12): LOCATE C, 10
B = 2
IF PHASERS <= 500 THEN B = 14
IF PHASERS <= 250 THEN B = 4
COLOR B: PRINT STR$(PHASERS); : COLOR A: C = C + 1
IF SYSTEM$ = "P" OR SYSTEM$ = "A" THEN COLOR 9: PRINT " *"

'--IMPULSE--
IF IDMG = 1 THEN A = 4: D = 14 ELSE A = 7: D = 15
LOCATE C, 2: COLOR D: PRINT "I"; : COLOR A: PRINT "MPULSE:";
PRINT SPACE$(12): LOCATE C, 10
B = 2
IF IMPULSE <= 250 THEN B = 14
IF IMPULSE <= 125 THEN B = 4
COLOR B: PRINT STR$(IMPULSE); : COLOR A: C = C + 1
IF SYSTEM$ = "I" OR SYSTEM$ = "A" THEN COLOR 9: PRINT " *"

'--WARP DRIVE--
IF WDMG = 1 THEN A = 4: D = 14 ELSE A = 7: D = 15
LOCATE C, 2: COLOR D: PRINT "W"; : COLOR A: PRINT "ARP DRIVE:";
PRINT SPACE$(9): LOCATE C, 13
B = 2
IF DRIVE <= 500 THEN B = 14
IF DRIVE <= 250 THEN B = 4
COLOR B: PRINT STR$(DRIVE); : COLOR A: C = C + 1
IF SYSTEM$ = "W" OR SYSTEM$ = "A" THEN COLOR 9: PRINT " *"
                                          
'--TORPEDOES--
IF TDMG = 1 THEN A = 4: D = 14 ELSE A = 7: D = 15
LOCATE C, 2: COLOR D: PRINT "T"; : COLOR A: PRINT "ORPEDOES:";
PRINT SPACE$(10): LOCATE C, 12
B = 2
IF TORPEDOES <= 4 THEN B = 14
IF TORPEDOES <= 2 THEN B = 4
COLOR B: PRINT STR$(TORPEDOES): COLOR A: C = C + 1

'--ENERGY--
D = 15: A = 7
LOCATE C, 2: COLOR D: PRINT "E"; : COLOR A: PRINT "NERGY:";
PRINT SPACE$(13): LOCATE C, 9
B = 2
IF ENERGY <= 2500 THEN B = 14
IF ENERGY <= 1250 THEN B = 4
COLOR B: PRINT STR$(ENERGY): COLOR A: C = C + 1

'--DAMAGE--
LOCATE C, 2: COLOR 7: PRINT "DAMAGE:";
PRINT SPACE$(13): LOCATE C, 9
B = 2
IF DAMAGE >= 50 THEN B = 14
IF DAMAGE >= 75 THEN B = 4
COLOR B: PRINT STR$(DAMAGE); "%": C = C + 1

'--STARDATES--
A = 3
B = 2
IF STRDT <= CINT(LSTRDT / 2) THEN B = 14
IF STRDT <= CINT(LSTRDT / 4) THEN B = 4
LOCATE C, 2: COLOR 7: PRINT "STARDATES:";
PRINT SPACE$(10): LOCATE C, 12
IF STRDT < 0 THEN STRDT = 0
COLOR B: PRINT CINT(STRDT) / 10

'--OMEGANS--
A = 3
C = C + 1
LOCATE C, 2: COLOR 7: PRINT "OMEGANS:";
PRINT "     ": LOCATE C, 10
COLOR 4: PRINT STR$(OMEGANS - KDSTRYD);
'PRINT ",";CHR$(48 + SECTOR(SECTORX, SECTORY))

'STARMAP

FOR Y = 0 TO 9
 LOCATE Y + 13, 27: COLOR 15: PRINT CHR$(48 + Y)
 FOR X = 0 TO 9
 LOCATE 12, X + 28: COLOR 15: PRINT CHR$(48 + X)
 LOCATE Y + 13, X + 28
 YY = Y + 13: XX = X + 29
 A = SECTOR(X, Y)
 COLOR 7
 IF SECTORX = X AND SECTORY = Y THEN
  PRINT " ";
  XX = XX - 1
  C0 = 1
  IF SHLDS$ = "Y" THEN C0 = 9
  IF A > 0 AND A < 9 THEN C0 = 14
  IF A = 9 THEN C0 = 2
  IF A = 11 THEN C0 = 5
  PSET (XX * 8 - 6, YY * 8 - 8), C0
  DRAW "S4 R2D1R1L4R1D1R2L1D4U1L2U1D3U2R4U1D3"
  COLOR 7: GOTO NEXTSEC
 END IF
 IF A = 0 THEN COLOR 7: PRINT CHR$(250)
 IF A > 0 AND A < 9 THEN
  C0 = 4
  PRINT " ";
  PSET (XX * 8 - 14, YY * 8 - 8), C0
  DRAW "S4 R2D1L2R1D3L2D1L1D2U1R1U1R4D1R1D1U2L1U1L2"
 END IF
 'IF A > 0 AND A < 9 THEN COLOR 4: PRINT CHR$(48 + A)
 IF A = 9 THEN COLOR 2: PRINT CHR$(4)
 IF A = 11 THEN COLOR 5: PRINT CHR$(4)
 COLOR 7
NEXTSEC:
NEXT: NEXT
CALL SHEILDSSUB
END SUB

SUB DOCKING
CALL DISPLAY
Z = SECTOR(SECTORX, SECTORY)
IF Z < 9 THEN GOTO DOCKEND
Z9 = 4: IF Z = 9 THEN Z9 = 2
PSET (155, 20), 8
DRAW "s16d1r3u1d1r1f1f1l1g1f1d1d1g1g1g1d1l1u1h3u2e1h1l1e2r1bd3p11,8"
PSET (155, 20), 8
DRAW "S16 BD3L3R9 BG1BL1L5 BG1R7 BD2L7 BF3R1"
PSET (155, 20), 8
PSET (155, 20), Z9: PSET (167, 20), Z9: PSET (161, 65), Z9
PAINT (157, 34), 3, 8
PAINT (157, 38), 3, 8
PAINT (157, 50), 3, 8
FOR TM = 1 TO 8000: NEXT
IF Z = 11 THEN GOTO DOCKEND
IF SHLDS$ = "Y" THEN SHEILDSC = 1: CALL SHEILDSSUB
ENERGY = 5000: SHEILDS = 1000: TORPEDOES = 10: PHASERS = 1000: DAMAGE = 0
DRIVE = 1000: IMPULSE = 500
LOCATE 12, 2: PRINT SPACE$(22)
LOCATE 12, 2: COLOR 15: PRINT "DOCKING"

FOR SND = 1 TO 5
FOR SS = 200 TO 250 STEP 1
SOUND SS, .2
NEXT SS
NEXT SND

FOR TM = 1 TO 10000: NEXT
LOCATE 12, 2: PRINT "ENERGY RESUPPLIED"

 SA = 500: SB = 750
 FOR SND = 1 TO 100
 FOR SS = SA TO SB STEP 50
 SOUND SS, .1
 NEXT SS
 SA = SA + 2: SB = SB + 2
 NEXT SND


STRDT = STRDT - 5
FOR TM = 1 TO 10000: NEXT
IF SYSDMG > 0 THEN
 LOCATE 12, 2: PRINT "REPAIRS COMPLETED"
 STRDT = STRDT - SYSDMG
 FOR SND = 1 TO 5
 FOR SS = 150 TO 100 STEP -1
 SOUND SS, .2
 NEXT SS
 NEXT SND
 FOR TM = 1 TO 10000: NEXT
 SYSDMG = 0: PDMG = 0: WDMG = 0: SDMG = 0: IDMG = 0: TDMG = 0
END IF
SECTOR(SECTORX, SECTORY) = 11
DOCKEND:
END SUB

SUB DRIVESUB

SX = SECTORX: SY = SECTORY
NEWSECTOR:
LOCATE 12, 2: PRINT SPACE$(22)
COLOR 7: LOCATE 12, 2: PRINT "ENTER SECTOR: ": COLOR 15
DO
X$ = INKEY$
IF X$ = CHR$(8) THEN GOTO DSUBEND
LOOP WHILE X$ = "" OR X$ < "0" OR X$ > "9"
LOCATE 12, 16
PRINT CHR$(VAL(X$) + 48); : PRINT ","
DO
Y$ = INKEY$
IF Y$ = CHR$(8) THEN GOTO DSUBEND
LOOP WHILE Y$ = "" OR Y$ < "0" OR Y$ > "9"
LOCATE 12, 18
PRINT CHR$(VAL(Y$) + 48)
SX1 = VAL(X$): SY1 = VAL(Y$)
FOR TM = 1 TO 6000: NEXT
MOVEMENT = 0
IF SX = SX1 AND SY = SY1 THEN GOTO DSUBEND
IF SX <> SX1 OR SY <> SY1 THEN
 MX = ABS(SX - SX1): MY = ABS(SY - SY1)
 IF MX > MY THEN MOVE = MX ELSE MOVE = MY
 MOVEMENT = MOVE: MOVE = MOVE * 200
 IF SHLDS$ = "Y" THEN MOVE = MOVE * 1.25
 IF WDMG = 1 THEN MOVE = CINT(MOVE * 2)
 IF MOVE > DRIVE THEN
  LOCATE 12, 2: PRINT SPACE$(22)
  LOCATE 12, 2: COLOR 15: PRINT "NOT ENOUGH ENERGY!"
 FOR TM = 1 TO 4000: NEXT
  MOVEMENT = 0
  GOTO DSUBEND
 END IF
 IF SECTOR(SX, SY) > 0 AND SECTOR(SX, SY) < 4 THEN CALL ENEMYFIRESUB
 SECTORY = VAL(Y$): SECTORX = VAL(X$)
 DRIVE = DRIVE - MOVE
 IF DRIVE <= 0 THEN DRIVE = 0
 FOR A = 1 TO 3
  KX(A) = 0: KY(A) = 0: KR(A) = 0: KC(A) = 0
 NEXT A
 LOCATE 12, 2: PRINT SPACE$(22)
 LOCATE 12, 2: COLOR 15: PRINT "ENGAGING WARP DRIVE!"
 SA = 37: SB = 150
 LINE (5, 5)-(315, 83), 0, BF
 FOR SND = 1 TO 200

XQ = 160: CQ = INT(RND(1) * 15) + 17
YQ = 40: DQ = INT(RND(1) * 8) + 1
IF DQ = 8 THEN LINE (XQ, YQ)-(5, 5), CQ
IF DQ = 6 THEN LINE (XQ, YQ)-(5, 83), CQ
IF DQ = 4 THEN LINE (XQ, YQ)-(315, 83), CQ
IF DQ = 2 THEN LINE (XQ, YQ)-(315, 5), CQ
IF DQ = 1 THEN LINE (XQ, YQ)-(XQ, 5), CQ
IF DQ = 3 THEN LINE (XQ, YQ)-(315, YQ), CQ
IF DQ = 5 THEN LINE (XQ, YQ)-(XQ, 83), CQ
IF DQ = 7 THEN LINE (XQ, YQ)-(5, YQ), CQ
DQ = INT(RND(1) * 8) + 1
IF DQ = 8 THEN LINE (XQ, YQ)-(5, 5), 0
IF DQ = 6 THEN LINE (XQ, YQ)-(5, 83), 0
IF DQ = 4 THEN LINE (XQ, YQ)-(315, 83), 0
IF DQ = 2 THEN LINE (XQ, YQ)-(315, 5), 0
IF DQ = 1 THEN LINE (XQ, YQ)-(XQ, 5), 0
IF DQ = 3 THEN LINE (XQ, YQ)-(315, YQ), 0
IF DQ = 5 THEN LINE (XQ, YQ)-(XQ, 83), 0
IF DQ = 7 THEN LINE (XQ, YQ)-(5, YQ), 0

 FOR SS = SA TO SB STEP 50
 SOUND SS, .1
 NEXT SS
 SA = SA + 2: SB = SB + 2
 NEXT SND
 LINE (5, 5)-(315, 83), 0, BF
 FOR Q = 1 TO 100
  STX(Q) = 0: STC(Q) = 0: STY(Q) = 0
 NEXT Q
 
FOR TM = 1 TO 4000: NEXT
 CALL STARSSUB
 NUMBER = SECTOR(SECTORX, SECTORY)
 SBX = 0: SBY = 0
 CALL DISPLAY
 IF NUMBER >= 9 THEN
  NUMBER = 0
  CALL DOCKING
 END IF
 IF NUMBER <> 0 THEN
  CALL OMEGANSSUB
  CALL ALARM
 END IF
END IF
STRDT = STRDT - MOVEMENT
DSUBEND:
IF MOVEMENT = 0 THEN NEXTTRN = 0 ELSE NEXTTRN = 2
CALL DISPLAY
IF NUMBER <> 0 THEN CALL ENEMYFIRESUB
IF NEXTTRN = 2 THEN CALL ENERGYSUB: CALL DISPLAY: NEXTTRN = 0
END SUB

SUB ENEMYFIRESUB

FOR K = 1 TO 3
 KMOVE(K) = 0: DM1 = 0: LEAVING(K) = 0
 IF KX(K) = 0 THEN GOTO NEXTKL
 Z1 = KSHLD(KN(K)): Z2 = KDMG(KN(K)): Z3 = Z1 - Z2
 IF Z3 < 0 THEN Z3 = 0
 'MOVE OR FIRE
 R = INT(RND(1) * 100) + 1
 IF Z2 >= Z1 * .75 THEN DM1 = 1
 IF DM1 = 1 AND R <= 60 AND KR(K) < 3 THEN KMOVE(K) = 1
 IF DM1 = 0 AND R <= 40 AND KR(K) > 1 THEN KMOVE(K) = 1
 'LEAVING
 IF DM1 = 1 THEN
  Q = INT(RND(1) * 100) + 1
  IF Q <= KR(K) * 20 THEN KMOVE(K) = 1: LEAVING(K) = 1: GOTO NEXTKL
 END IF
 IF KMOVE(K) = 1 THEN GOTO NEXTFIRE

 LOCATE 12, 2: PRINT SPACE$(22)
 LOCATE 12, 2: COLOR 7
 PRINT "OMEGAN"; : COLOR 15: PRINT K; : COLOR 7: PRINT "FIRING!"
 FOR TM = 1 TO 4000: NEXT
 DM1 = 0: IF Z2 > Z1 * .75 THEN DM1 = 1
 KF = 20 + (KTYPE(KN(K)) * 20)
 IF KR(K) = 1 AND DM1 = 0 THEN KF = CINT(KF * .5)
 IF KR(K) = 2 AND DM1 = 0 THEN KF = CINT(KF * .75)
 IF DM1 = 1 THEN KF = CINT(KF * .25)
 G = INT(RND(1) * 100) + 1: TORP = 0
 IF G > KF THEN GOTO DISRUPTOR

'TORPEDO

 TX = KX(K): TY = KY(K)
 AA = 2000: BB = 37
 FOR SND = 1 TO 100
  FOR SS = AA TO BB STEP -100
   SOUND SS, .05
  NEXT SS
  AA = AA - 100
 NEXT SND

 FG = 0
 FOR TF = TY * 8 - 1 TO 78 STEP 4
  IF FG = 1 THEN FH = 42 ELSE FH = 43
  FG = FG + 1: IF FG > 1 THEN FG = 0
  PSET (TX * 8 + 2, TF), 4
  DRAW "S4R1D2L1U1L1R3"
  PSET (TX * 8, TF - 1), FH
  DRAW "S4 F1 BR3E1G1 BD2F1H1 BL3G1"

  FOR TM = 1 TO 200: NEXT
  PSET (TX * 8 + 2, TF), 0
  DRAW "S4R1D2L1U1L1R3"
  PSET (TX * 8, TF - 1), 0
  DRAW "S4 F1 BR3E1G1 BD2F1H1 BL3G1"
 NEXT TF
 CALL STARSSUB
 HIT = 105 - (KR(K) * 10)
 V = INT(RND(1) * 100) + 1
 IF V > HIT THEN Z3 = 0 ELSE Z3 = 500
 TORP = 1

'DISRUPTOR

DISRUPTOR:
 DSRPTR = Z3
 RNDN = DSRPTR: CALL RAND: DH = RNDM(3): DM = RNDM(2): DL = RNDM(1)
 IF KR(K) = 1 THEN HIT = DH
 IF KR(K) = 2 THEN HIT = DM
 IF KR(K) = 3 THEN HIT = DL
 IF TORP = 1 THEN HIT = DH
 DD = 0: SH = SHEILDS - (HIT / 2)
 IF SDMG = 1 THEN SH = SH / 2
 DMG = INT(RND(1) * 1000) + 1
 IF DMG > SH THEN
  DD = CINT((HIT * (1 - (SH / DMG))) / 10)
 END IF
 IF Z3 = 0 THEN DD = 0: HIT = 0
 IF SHLDS$ = "N" THEN DD = CINT(HIT / 10)
 IF SHEILDS - HIT < 0 THEN HIT = SHEILDS
 IF SHLDS$ = "Y" THEN SHEILDS = SHEILDS - HIT
 IF SHEILDS < 0 THEN SHEILDS = 0
 DAMAGE = DAMAGE + DD

 IF TORP = 0 THEN
  TY = KY(K)
  AA = 2800: BB = 37
  FOR SND = 1 TO 15
   FOR SS = AA TO BB STEP -150
    SOUND SS, .05
   NEXT SS
   AA = AA - 150
  NEXT SND

  XL = 8: XR = 13: XX = 7 / (78 - (KY(K) * 8)) * 5: X9 = 0
  M0 = POINT(KX(K) * 8 - XL, KY(K) * 8 - 1)
  FG = 0
  FOR TF = TY * 8 - 1 TO 78 STEP 5
   IF FG = 1 THEN FH = 42 ELSE FH = 43
   FG = FG + 1: IF FG > 1 THEN FG = 0
   PSET (KX(K) * 8 - XL, TF), 4
   DRAW "S2R1D2R2D4L4U4R2D2U4"
   PSET (KX(K) * 8 - XL, TF + 2), FH
   PSET (KX(K) * 8 + XR, TF), 4
   DRAW "S2R1D2R2D4L4U4R2D2U4"
   PSET (KX(K) * 8 + XR, TF + 2), FH
   FOR TM = 1 TO 200: NEXT
   PSET (KX(K) * 8 - XL, TF), 0
   DRAW "S2R1D2R2D4L4U4R2D2U4"
   PSET (KX(K) * 8 + XR, TF), 0
   DRAW "S2R1D2R2D4L4U4R2D2U4"
   XL = XL - XX: XR = XR - XX
   IF X9 = 0 THEN
    PSET (KX(K) * 8 - 8, KY(K) * 8 - 1), M0
    PSET (KX(K) * 8 + 13, KY(K) * 8 - 1), M0
    X9 = 1
   END IF
  NEXT TF
  CALL STARSSUB
 END IF

 IF DD > 0 THEN
   PSET (KX(K) * 8, 74), 14
   DRAW "s4r3d1r2u1r2d1r1d2l1d2r1d1l1d1l3u1l2d1l2u1l1u2r1u2l1u1r1u1bf3p14,14"
  ELSEIF TORP = 0 OR Z3 <> 0 THEN
   PSET (KX(K) * 8, 74), 48
   DRAW "s4r3d1r2u1r2d1r1d2l1d2r1d1l1d1l3u1l2d1l2u1l1u2r1u2l1u1r1u1bf3p48,48"
 END IF

 IF DD > 0 THEN
   OX = KX(K) * 8 - 2: OY = 74
   FOR OZ = 1 TO 20
P101:
   OOX = INT(RND(1) * 11): OOY = INT(RND(1) * 9)
   OOX = OOX + OX: OOY = OOY + OY
   IF POINT(OOX, OOY) <> 14 THEN GOTO P101
   PSET (OOX, OOY), 8
   NEXT OZ
 END IF
 CALL STARSSUB

 IF DAMAGE >= 100 THEN CALL SHIPDESTROYED
 NN = 0
 IF Z3 > 0 THEN NN = 500
 IF Z3 > 0 AND DD > 0 THEN NN = 750
 IF NN > 0 THEN
  FOR ZZ = 1 TO NN
   SND = INT(RND(1) * NN) + 37
   SOUND SND, .025
  NEXT ZZ
 END IF
 PSET (KX(K) * 8, 74), 0
 DRAW "s4r3d1r2u1r2d1r1d2l1d2r1d1l1d1l3u1l2d1l2u1l1u2r1u2l1u1r1u1bf3p0,0"
 CALL STARSSUB

 IF SHLDS$ = "Y" AND Z3 > 0 THEN
  LOCATE 12, 2: PRINT SPACE$(22)
  LOCATE 12, 2: COLOR 7: PRINT "SHEILDS HIT:"; : COLOR 15: PRINT STR$(HIT)
  FOR TM = 1 TO 6000: NEXT
 END IF
 IF DD > 0 THEN
  LOCATE 12, 2: PRINT SPACE$(22)
  LOCATE 12, 2: COLOR 7: PRINT "SHIP DAMAGE:"; : COLOR 15: PRINT STR$(DD); "%"
  FOR TM = 1 TO 6000: NEXT
 END IF
 J99 = INT((DAMAGE - 10) / 15)
 IF SYSDMG < J99 AND J99 > 0 THEN CALL DAMAGESUB
 CALL DISPLAY

NEXTFIRE:
 IF KMOVE(K) = 0 THEN GOTO NEXTKL
 IF DM1 = 1 THEN
  KR(K) = KR(K) + 1
  K$ = "EVADING!"
  EVADING(K) = 1
 END IF
 IF DM1 = 0 THEN
  KR(K) = KR(K) - 1
  K$ = "CLOSING!"
  EVADING(K) = 0
 END IF
 LOCATE 12, 2: PRINT SPACE$(22): LOCATE 12, 2
 COLOR 7: PRINT "OMEGAN"; : COLOR 15: PRINT K; : COLOR 7: PRINT ; K$
 FOR TM = 1 TO 6000: NEXT
 CALL SHIPSSUB
NEXTKL:
NEXT K
END SUB

SUB ENERGYSUB
IF COMMANDS$ = "E" THEN
 LOCATE 12, 2: PRINT SPACE$(22)
 COLOR 7: LOCATE 12, 2
 PRINT "SYSTEM: "; : COLOR 15
 A$ = ""
 DO
  A$ = INKEY$
 LOOP WHILE A$ <> "S" AND A$ <> "P" AND A$ <> "W" AND A$ <> "I" AND A$ <> "N" AND A$ <> "A" AND A$ <> CHR$(8)
 IF A$ = CHR$(8) THEN
 GOTO ENERGYEND
 ELSEIF A$ = "S" THEN
 PRINT "SHEILDS"
 ELSEIF A$ = "P" THEN
 PRINT "PHASERS"
 ELSEIF A$ = "W" THEN
 PRINT "WARP DRIVE"
 ELSEIF A$ = "I" THEN
 PRINT "IMPULSE"
 ELSEIF A$ = "N" THEN
 PRINT "NONE"
 ELSEIF A$ = "A" THEN
 PRINT "ALL SYSTEMS"
 END IF
FOR TM = 1 TO 4000: NEXT
 SYSTEM$ = LEFT$(A$, 1)
 COMMANDS$ = ""
 GOTO ENERGYEND
END IF

DATEOLD = DATENEW: DATENEW = STRDT: DATE = ABS(DATEOLD - DATENEW)
IF DATE <= 0 THEN DATE = 0
EE = 100 * DATE: IF SYSTEM$ = "N" THEN EE = 250 * DATE
ENERGY = ENERGY + EE
IF ENERGY >= 5000 THEN ENERGY = 5000
RE = 250 * DATE
IF SYSTEM$ <> "N" THEN
 LOCATE 12, 2: PRINT SPACE$(22);
 COLOR 15
 LOCATE 12, 2
 PRINT "RECHARGING SYSTEMS"
 FOR TM = 1 TO 4000: NEXT
END IF
IF SYSTEM$ = "A" THEN RE = 100 * DATE
'IF SHLDS$ = "Y" THEN RE = CINT(RE / 2)
IF SYSTEM$ = "N" THEN RE = 0: GOTO ENERGYEND
IF SYSTEM$ = "S" OR SYSTEM$ = "A" THEN GOSUB E1: GOSUB E2: GOSUB E3: GOSUB E4
IF SYSTEM$ = "P" THEN GOSUB E2: GOSUB E3: GOSUB E4: GOSUB E1
IF SYSTEM$ = "I" THEN GOSUB E3: GOSUB E4: GOSUB E1: GOSUB E2
IF SYSTEM$ = "W" THEN GOSUB E4: GOSUB E1: GOSUB E2: GOSUB E3
IF ENERGY > 5000 THEN ENERGY = 5000
IF ENERGY < 0 THEN ENERGY = 0
GOTO ENERGYEND
E1:
IF SHEILDS < 1000 THEN
 IF SHEILDS <= 0 THEN SHEILDS = 0
 AMT = 0
 IF SYSTEM$ = "S" OR SYSTEM$ = "A" THEN AMT = RE
 IF AMT + SHEILDS > 1000 THEN AMT = 1000 - SHEILDS
 IF AMT > ENERGY THEN AMT = ENERGY
 SHEILDS = SHEILDS + AMT
 ENERGY = ENERGY - AMT
END IF
RETURN
E2:
IF PHASERS < 1000 THEN
 IF PHASERS <= 0 THEN PHASERS = 0
 AMT = 0
 IF SYSTEM$ = "P" OR SYSTEM$ = "A" THEN AMT = RE
 IF AMT + PHASERS > 1000 THEN AMT = 1000 - PHASERS
 IF AMT > ENERGY THEN AMT = ENERGY
 PHASERS = PHASERS + AMT
 ENERGY = ENERGY - AMT
END IF
RETURN
E3:
IF IMPULSE < 500 THEN
 IF IMPULSE <= 0 THEN IMPULSE = 0
 AMT = 0
 IF SYSTEM$ = "I" OR SYSTEM$ = "A" THEN AMT = RE
 IF AMT + IMPULSE > 500 THEN AMT = 500 - IMPULSE
 IF AMT > ENERGY THEN AMT = ENERGY
 IMPULSE = IMPULSE + AMT
 ENERGY = ENERGY - AMT
END IF
RETURN
E4:
IF DRIVE < 1000 THEN
 IF DRIVE <= 0 THEN DRIVE = 0
 AMT = 0
 IF SYSTEM$ = "W" OR SYSTEM$ = "A" THEN AMT = RE
 IF AMT + DRIVE > 1000 THEN AMT = 1000 - DRIVE
 IF AMT > ENERGY THEN AMT = ENERGY
 DRIVE = DRIVE + AMT
 ENERGY = ENERGY - AMT
 IF DRIVE <= 0 THEN DRIVE = 0
END IF
RETURN
ENERGYEND:
END SUB

SUB IMPULSESUB

LOCATE 12, 2: PRINT SPACE$(22)
LOCATE 12, 2
COLOR 15: PRINT "P"; : COLOR 7: PRINT "URSUIT OR ";
COLOR 15: PRINT "E"; : COLOR 7: PRINT "VADE"
DO
A$ = INKEY$
IF A$ = CHR$(8) THEN GOTO ENDIMPULSE
LOOP WHILE A$ = "" AND A$ <> "P" AND A$ <> "E"
IM = 125
IF IDMG = 1 THEN IM = IM * 2
IF IM > IMPULSE THEN
 LOCATE 12, 2: PRINT SPACE$(22)
 LOCATE 12, 2: COLOR 15: PRINT "NOT ENOUGH ENERGY!"
 FOR TM = 1 TO 4000: NEXT
 GOTO ENDIMPULSE
END IF
IMPULSE = IMPULSE - IM
FOR TM = 1 TO 6000: NEXT
LOCATE 12, 2: PRINT SPACE$(22)
IF A$ = "P" THEN
 COLOR 7
 LOCATE 12, 2: PRINT "ENEMY SHIP: "; : COLOR 15
 DO
 A$ = INKEY$
 IF A$ = CHR$(8) THEN IMPULSE = IMPULSE + IM: GOTO ENDIMPULSE
 LOOP WHILE A$ = "" OR A$ < "0" OR KX(VAL(A$)) = 0
 PRINT A$: FOR TM = 1 TO 4000: NEXT
 LOCATE 12, 2: COLOR 15: PRINT SPACE$(22)
 LOCATE 12, 2: PRINT "PURSUING ENEMY SHIP"
 SA = 100: SB = 300
 FOR SND = 1 TO 200
 FOR SS = SA TO SB STEP 100
 SOUND SS, .05
 NEXT SS
 SA = SA + .5: SB = SB + .5
 NEXT SND
 FOR TM = 1 TO 4000: NEXT
 A = VAL(A$)
 KR(A) = KR(A) - 1
 IF KR(A) < 1 THEN KR(A) = 1
ELSE
 LOCATE 12, 2: COLOR 15: PRINT "EVADING ENEMY SHIPS"
 SA = 100: SB = 300
 FOR SND = 1 TO 200
 FOR SS = SA TO SB STEP 100
 SOUND SS, .05
 NEXT SS
 SA = SA + .5: SB = SB + .5
 NEXT SND
 FOR TM = 1 TO 4000: NEXT
 FOR B = 1 TO 3
  IF KR(B) > 0 THEN KR(B) = KR(B) + 1
  IF KR(B) > 3 THEN KR(B) = 3
 NEXT B
END IF
NEXTTRN = 1
CALL SHIPSSUB
ENDIMPULSE:
END SUB

SUB INSTRUCTIONS
CLS
SCREEN 0
WIDTH 80, 43
CALL REGISTRATION
COLOR 13: PRINT "INSTRUCTIONS"
PRINT
PRINT "THIS GAME IS A FORWARD VIEW STARSHIP BATTLE SIMULATION. YOUR MISSION"
PRINT "IS TO PROTECT THE STARBASES BY DESTROYING THE ENEMY OMEGAN STARSHIPS WITHIN"
PRINT "THE ALOTTED STARDATES. THERE ARE FOUR DIFFERENT ENEMY SHIPS."
PRINT "(BROWN - BATTLESHIPS, PURPLE - CRUISERS, BLUE - DESTROYERS, GREEN - FRIGATES)"
PRINT "***THIS VERSION INCLUDES ONLY THE FRIGATES***"
PRINT "THE COMBAT DISPLAY WILL SHOW THE RANGE, TARGET NUMBER, AND SHEILD RATING OF"
PRINT "ENEMY SHIPS. THE RANGES ARE S-SHORT, M-MEDIUM, L-LONG. WEAPONS ARE MORE"
PRINT "EFFECTIVE AT SHORTER RANGES. STARBASES WILL REPAIR AND RECHARGE ALL SYSTEMS."
PRINT
PRINT "THE COMMANDS ARE:"
PRINT "S-SHEILDS; RAISES AND LOWERS SHEILDS"
PRINT "P-PHASERS; FIRES PHASERS"
PRINT "  ENTER THE AMOUNT YOU WISH TO FIRE AT THE TARGET"
PRINT "T-TORPEDOES; FIRES UP TO TWO TORPEDOES"
PRINT "  ENTER THE TARGET NUMBER OR 0 IF YOU DO NOT WISH TO FIRE"
PRINT "I-IMPULSE; WILL ALLOW YOU TO PURSUE OR EVADE ENEMY SHIPS"
PRINT "W-WARP DRIVE; WILL ENGAGE WARP DRIVE"
PRINT "  WARP DRIVE WILL USE INCREASED ENERGY WITH SHEILDS UP"
PRINT "E-ENERGY; WILL ALLOW YOU TO SELECT THE SYSTEMS TO RECHARGE"
PRINT "  A STAR WILL APPEAR NEXT TO THE SYSTEMS RECHARGING"
PRINT "  USE THE FIRST LETTER TO SELECT THE SYSTEM, N FOR NO RECHARGE, OR A FOR ALL"
PRINT "  (TORPEDOES WILL USE 100 UNITS OF ENERGY WHEN FIRED)"
PRINT "TAB-WAIT ONE TURN OR USE MAX PHASERS WHEN TARGETING PHASER FIRE"
PRINT "BACKSPACE-ABORT PRESENT COMMAND"
PRINT
PRINT "RECHARGE RATE PER STARDATE:"
PRINT "250 SINGLE SYSTEM RECHARGE, 100 FOR ALL SYSTEMS RECHARGE"
PRINT "100 MAIN ENERGY RECHARGE, 250 IF NO SYSTEMS ARE RECHARGING"
PRINT
PRINT "THE STARMAP CODES ARE:"
PRINT "BLUE-YOUR SHIP; RED-OMEGANS; GREEN-STARBASES; PURPLE-USED STARBASES"
PRINT
PRINT "SYSTEM DAMAGE:"
PRINT "SHEILDS-SHEILDS EFFECTIVENESS WILL BE HALVED"
PRINT "PHASERS-PHASER DAMAGE WILL BE HALVED"
PRINT "TORPEDOES-1 TUBE WILL BE OUT"
PRINT "WARP DRIVE AND IMPULSE-ENERGY DRAIN WILL BE DOUBLED"
PRINT
PRINT "HIT ANY KEY"

DO
A$ = INKEY$
LOOP WHILE A$ = ""
WIDTH 40, 25
SCREEN 13
CLS
END SUB

SUB INTRO

'FED STARSHIP
PSET (110, 30), 8
DRAW "S20 R2F1L4R8F2L5G1L4H1R6L11E2R4E1 BR5BD3 F1R4L4D2F1R5U1R7E1H1L9H1"
DRAW "BR5BD1 U1L2H1R15D1L12R2D1"
PAINT (110, 31), 1, 8
PAINT (110, 38), 9, 8
PAINT (110, 47), 1, 8
PAINT (140, 47), 1, 8
PAINT (185, 47), 1, 8
PAINT (185, 43), 9, 8
PAINT (185, 53), 9, 8

'OMEGAN
PSET (90, 110), 8
DRAW "S22 R1D1R2D1L5U1R2U1R1BD1"
DRAW "BD1R1F1D1G1L3H1U1E1R3"
DRAW "BF1R12D1L12U1"
DRAW "BR12E1R5U1R3D1L3R4D3L9H1"
DRAW "F1R1F1L6D1R14U1L8H1R8F1"
PAINT (92, 112), 2, 8
PAINT (92, 123), 10, 8
PAINT (110, 127), 2, 8
PAINT (180, 127), 10, 8
PAINT (200, 138), 2, 8
PAINT (210, 145), 10, 8
PAINT (210, 117), 2, 8


'TITLE
LOCATE 12, 17: COLOR 5: PRINT "STARSHIP"
LOCATE 23, 16: COLOR 13: PRINT "HIT ANY KEY"

'STARS
 DIM GX(500), GY(500)
 FOR G = 1 TO 500
  DO
  X = INT(RND(1) * 319) + 1: Y = INT(RND(1) * 199) + 1
  GX(G) = X: GY(G) = Y
  LOOP UNTIL POINT(X, Y) = 0
  PSET (X, Y), INT(RND(1) * 15) + 17
 NEXT G
 
G = 0
DO
A$ = INKEY$
G = G + 1: IF G = 501 THEN G = 1
PSET (GX(G), GY(G)), INT(RND(1) * 15) + 17
LOOP UNTIL A$ <> ""
END SUB

SUB KDESTROYED
 LOCATE KY(KHIT), KX(KHIT) + 1
 LOCATE 12, 2: PRINT SPACE$(22)
 LOCATE 12, 2: COLOR 15: PRINT "OMEGAN DESTROYED!"
 SCORE = SCORE + KTYPE(KN(KHIT)) * 250
 COLOR 12
'KILL
 PSET (KX(KHIT) * 8 - 3, KY(KHIT) * 8 - 9), 4
  DRAW "s7r3d1r2u1r2d1r1d2l1d2r1d1l1d1l3u1l2d1l2u1l1u2r1u2l1u1r1u1bf1p4,4"
'EXPLOSION
 PSET (KX(KHIT) * 8 - 1, KY(KHIT) * 8 - 8), 14
  DRAW "s4r3d1r2u1r2d1r1d2l1d2r1d1l1d1l3u1l2d1l2u1l1u2r1u2l1u1r1u1bf1p14,14"

 
   OX = KX(KHIT) * 8 - 4: OY = KY(KHIT) * 8 - 9
   FOR OZ = 1 TO 30
P102:
   OOX = INT(RND(1) * 15): OOY = INT(RND(1) * 17)
   OOX = OOX + OX: OOY = OOY + OY
   IF POINT(OOX, OOY) <> 14 AND POINT(OOX, OOY) <> 4 THEN GOTO P102
   PSET (OOX, OOY), 8
   NEXT OZ
 
 FOR Z = 1 TO 1000
  SND = INT(RND(1) * 500) + 37
  SOUND SND, .025
 NEXT Z
FOR TM = 1 TO 4000: NEXT
 LOCATE KY(KHIT), KX(KHIT) - 1: PRINT "    "
 LOCATE KY(KHIT) - 1, KX(KHIT): PRINT "    "
 LOCATE KY(KHIT) + 1, KX(KHIT) - 1: PRINT "     "
 KDSTRYD = KDSTRYD + 1: N = KN(KHIT)
 KY(KHIT) = 0: KX(KHIT) = 0: KC(KHIT) = 0:
 KSHLD(N) = 0: KTYPE(N) = 0: KCLR(N) = 0: KDMG(N) = 0
 KSX(N) = 0: KSY(N) = 0: KR(KHIT) = 0: KN(KHIT) = 0
 SECTOR(SECTORX, SECTORY) = SECTOR(SECTORX, SECTORY) - 1
 IF SECTOR(SECTORX, SECTORY) < 0 THEN SECTOR(SECTORX, SECTORY) = 0
 CALL STARSSUB
END SUB

SUB KLMOVE
FOR A = 1 TO OMEGANS
IF KSHLD(A) = 0 THEN GOTO NEXTKLNG
L = 5: M = INT(RND(1) * 100) + 1: A1 = 0: R = 0

'--LEAVING--
LEAVING = 0
FOR B = 1 TO 3
 IF KN(B) = A AND LEAVING(B) = 1 THEN LEAVING = 1: A1 = B
NEXT B
IF LEAVING = 0 AND KSX(A) = SECTORX AND KSY(A) = SECTORY THEN GOTO NEXTKLNG
IF LEAVING = 1 THEN
 L = 100
 LOCATE 12, 2: PRINT SPACE$(22)
 EVADING(A1) = 1
 COLOR 7: LOCATE 12, 2
 PRINT "OMEGAN"; : COLOR 15: PRINT A1; : COLOR 7: PRINT "LEAVING!"
 CALL SHIPSSUB
 FOR TM = 1 TO 6000: NEXT
 LOCATE KY(A1), KX(A1) - 1: PRINT "    "
 LOCATE KY(A1) + 1, KX(A1) - 1: PRINT " "
 LOCATE KY(A1) + 1, KX(A1) + 3: PRINT " "
 LOCATE KY(A1) - 1, KX(A1): PRINT "    "
 CALL STARSSUB
 KX(A1) = 0: KY(A1) = 0: KR(A1) = 0: KC(A1) = 0: KN(A1) = 0
END IF

'--NEW LOCATION--
IF M <= L AND KSHLD(A) > 0 THEN
RERUN:
 X = INT(RND(1) * 10): Y = INT(RND(1) * 10)
 J = INT(RND(1) * 100) + 1
 IF J < 10 THEN X = SECTORX: Y = SECTORY
 IF SECTOR(X, Y) >= 3 THEN GOTO RERUN
 IF KSX(A) = X AND KSY(A) = Y THEN GOTO RERUN
 IF KDMG(A) >= KSHLD(A) * .75 AND SECTORX = X AND SECTORY = Y THEN GOTO RERUN
 SECTOR(KSX(A), KSY(A)) = SECTOR(KSX(A), KSY(A)) - 1
 SECTOR(X, Y) = SECTOR(X, Y) + 1
 KSX(A) = X: KSY(A) = Y

'--OMEGAN ARRIVING--
 IF SECTORX = X AND SECTORY = Y THEN
NEWXY1:
  XX = 4 + INT(RND(1) * 32): YY = 4 + INT(RND(1) * 3)
  FOR D = 1 TO 3
   IF KX(D) > 0 THEN
    X4 = KX(D) + 1
    X1 = ABS(X4 - XX)
    IF X1 < 6 THEN GOTO NEWXY1
   END IF
  NEXT D
  P = 0
  DO
   P = P + 1
  LOOP WHILE KX(P) <> 0
  KX(P) = XX: KY(P) = YY: KN(P) = A: KC(P) = KCLR(P)
  R = INT(RND(1) * 3) + 1: KR(P) = R
  LOCATE 12, 2: PRINT SPACE$(22)
  LOCATE 12, 2: COLOR 7
  PRINT "OMEGAN"; : COLOR 15: PRINT P; : COLOR 7: PRINT "ARRIVING!"
  EVADING(P) = 0
  CALL ALARM
  CALL SHIPSSUB
  FOR TM = 1 TO 4000: NEXT
 END IF
END IF
NEXTKLNG:
NEXT A
END SUB

SUB KREPAIRS
FOR A = 1 TO OMEGANS
IF KSHLD(A) = 0 OR KDMG(A) = 0 THEN GOTO KRPR
 V = INT(RND(1) * 100) + 1
 IF V > 20 THEN GOTO KRPR
 R = CINT(KSHLD(A) * .25)
 RNDN = R: CALL RAND: RPR = RNDM(3)
 KDMG(A) = KDMG(A) - RPR
 IF KDMG(A) <= 0 THEN KDMG(A) = 0
KRPR:
NEXT A
END SUB

SUB LEVELSUB

CLS
COLOR 13
PRINT "         CHOOSE DIFFICULTY LEVEL"
PRINT
PRINT
COLOR 13: PRINT "1> EASY"
COLOR 5: PRINT "10 OMEGAN STARSHIPS"
PRINT "1 STARBASE, 5 STARDATES"
PRINT : PRINT
COLOR 7: PRINT "2> MODERATE"
COLOR 8: PRINT "20 OMEGAN STARSHIPS"
PRINT "2 STARBASES, 10 STARDATES"
PRINT : PRINT
COLOR 7: PRINT "3> HARD"
COLOR 8: PRINT "30 OMEGAN STARSHIPS"
PRINT "3 STARBASES, 15 STARDATES"
PRINT : PRINT
COLOR 7: PRINT "4> EXTREME"
COLOR 8: PRINT "40 OMEGAN STARSHIPS"
PRINT "4 STARBASES, 20 STARDATES"
DO
A$ = INKEY$
LOOP UNTIL A$ = "1"
IF A$ = "1" THEN STARBASES = 1: OMEGANS = 10: STRDT = 50
LSTRDT = STRDT
LEVEL = VAL(A$)
END SUB

SUB MAPSETUP


FOR A = 1 TO STARBASES
NEWLOC:
DO
 X = INT(RND(1) * 10): Y = INT(RND(1) * 10)
LOOP WHILE SECTOR(X, Y) <> 0
FOR B = 1 TO STARBASES
 XX = ABS(SBX(B) - X): YY = ABS(SBY(B) - Y)
 IF XX > YY THEN WW = XX ELSE WW = YY
 IF WW < 3 THEN GOTO NEWLOC
NEXT
 SECTOR(X, Y) = 9: SBX(A) = X: SBY(A) = Y
NEXT

NEWSEC:
DO
 X = INT(RND(1) * 10): Y = INT(RND(1) * 10)
LOOP WHILE SECTOR(X, Y) <> 0
FOR B = 1 TO 3
 XX = ABS(SBX(B) - X): YY = ABS(SBY(B) - Y)
 IF XX > YY THEN WW = XX ELSE WW = YY
 IF WW < 5 THEN GOTO NEWSEC
NEXT
 SECTORX = X: SECTORY = Y

FOR A = 1 TO OMEGANS
XY:
DO
 X = INT(RND(1) * 10): Y = INT(RND(1) * 10)
LOOP WHILE SECTOR(X, Y) >= 3
IF X = SECTORX AND Y = SECTORY THEN GOTO XY
SECTOR(X, Y) = SECTOR(X, Y) + 1
KSX(A) = X: KSY(A) = Y
NEWK:
IF LEVEL = 1 THEN B = 1
IF B = 1 THEN
 KTYPE(A) = 1: KSHLD(A) = 400
 KCLR(A) = 2
END IF
NEXT A

END SUB

SUB OMEGANSSUB

P = 0
FOR A = 1 TO OMEGANS
IF KSX(A) = SECTORX AND KSY(A) = SECTORY AND KTYPE(A) <> 0 THEN
 P = P + 1
NEWXY:
 X = 4 + INT(RND(1) * 32): Y = 4 + INT(RND(1) * 3)
 FOR D = 1 TO 3
  IF KX(D) > 0 THEN
   X4 = KX(D) + 1
   X1 = ABS(X4 - X)
   IF KX(D) > 0 THEN
    IF X1 < 6 THEN GOTO NEWXY
   END IF
  END IF
 NEXT D
 EVADING(P) = 0
 KX(P) = X: KY(P) = Y: KN(P) = A: KC(P) = KCLR(A)
 R = INT(RND(1) * 3) + 1: KR(P) = R
END IF
NEXT A
CALL SHIPSSUB
END SUB

SUB PHASERSSUB

PHASERSTART:
FOR X = 1 TO 3
PHASERS(X) = 0: NEXT
FOR XXX = 1 TO 3
IF KX(XXX) = 0 THEN GOTO NEXTTRGT
PHASERAMT:
AMT$ = ""
LOCATE 12, 2: PRINT SPACE$(22)
LOCATE 12, 2: COLOR 7
PRINT "TARGET "; CHR$(48 + XXX); ": "; : COLOR 15
DO
 DO
  A$ = INKEY$
  FIRING = 0
 IF A$ = CHR$(9) THEN
  A10 = 0
  FOR A9 = 1 TO 3
  IF KX(A9) > 0 THEN A10 = A10 + PHASERS(A9)
  NEXT A9
  A$ = STR$(PHASERS - A10): Z = LEN(A$)
  AMT$ = MID$(A$, 2, Z)
  FIRING = 1
  GOTO PAMT
 END IF
 IF A$ = CHR$(13) THEN : GOTO PAMT
 IF A$ = CHR$(8) THEN GOTO PHASEREND
 LOOP WHILE A$ < "0" OR A$ > "9"
 AMT$ = AMT$ + A$
 L = LEN(AMT$): IF L > 4 THEN GOTO PHASERAMT
 LOCATE 12, 12: PRINT AMT$
LOOP WHILE L < 5
PAMT:
 AMT = VAL(AMT$)
 LOCATE 12, 12: PRINT AMT$
 IF AMT > 1000 THEN GOTO PHASERAMT
 TOT = 0
 PHASERS(XXX) = AMT
 FOR Q = 1 TO 3
  TOT = TOT + PHASERS(Q)
 NEXT Q
 IF TOT > PHASERS OR TOT > 1000 THEN
  PHASERS(XXX) = 0
  GOTO PHASERAMT
 END IF
FOR TM = 1 TO 4000: NEXT
 IF FIRING = 1 THEN GOTO FIREBEGIN
NEXTTRGT:
NEXT XXX
FIREBEGIN:
PHASERS = PHASERS - TOT

'PHASERS FIRING

LOCATE 12, 2: PRINT SPACE$(22)
FOR FF = 1 TO 3
IF PHASERS(FF) = 0 THEN GOTO NEXTFIRING
LOCATE 12, 2: PRINT SPACE$(22)
LOCATE 12, 2
COLOR 15: PRINT "FIRING PHASERS!"
FOR TM = 1 TO 2000: NEXT
XX = KX(FF) + 1: K = FF
LINE (KX(K) * 8 + 2, 83)-(KX(K) * 8 + 2, KY(K) * 8 - 1), 4
LINE (KX(K) * 8 + 3, 83)-(KX(K) * 8 + 3, KY(K) * 8 - 1), 4
DP = PHASERS(FF): DR = KR(FF)
RNDN = DP: CALL RAND: DH = RNDM(3): DM = RNDM(2): DL = RNDM(1)
IF PDMG = 1 THEN
 DH = CINT(DH * .5): DL = CINT(DL * .5): DM = CINT(DM * .5)
END IF
KD = KDMG(KN(FF)): KS = KSHLD(KN(FF))
IF DR = 1 THEN KD = KD + DH: DD = DH
IF DR = 2 THEN KD = KD + DM: DD = DM
IF DR = 3 THEN KD = KD + DL: DD = DL
KDMG(KN(FF)) = KDMG(KN(FF)) + DD
KD = KDMG(KN(FF)): KS = KSHLD(KN(FF))
DF = INT(RND(1) * 100) + 1
DE = 0
IF KDMG(K) >= KSHLD(K) * .5 THEN DE = 2
IF KDMG(K) >= KSHLD(K) * .75 THEN DE = 3
IF DF >= 100 - (DE * 25) OR KD >= KS THEN
 PSET (KX(FF) * 8 - 1, KY(FF) * 8 - 8), 14
 DRAW "s4r3d1r2u1r2d1r1d2l1d2r1d1l1d1l3u1l2d1l2u1l1u2r1u2l1u1r1u1bf3p14,14"
 OX = KX(FF) * 8 - 2: OY = KY(FF) * 8 - 8
 FOR OZ = 1 TO 20
P100:
 OOX = INT(RND(1) * 11): OOY = INT(RND(1) * 9)
 OOX = OOX + OX: OOY = OOY + OY
 IF POINT(OOX, OOY) <> 14 THEN GOTO P100
 PSET (OOX, OOY), 8
 NEXT OZ
ELSE
 PSET (KX(FF) * 8 - 1, KY(FF) * 8 - 8), 48
 DRAW "s4r3d1r2u1r2d1r1d2l1d2r1d1l1d1l3u1l2d1l2u1l1u2r1u2l1u1r1u1bf3p48,48"
END IF

C0 = 0
FOR SND = 1 TO 25
C0 = C0 + 1: IF C0 > 1 THEN C0 = 0
IF C0 = 0 THEN C1 = 4
IF C0 = 1 THEN C1 = 12
LINE (KX(K) * 8 + 2, 83)-(KX(K) * 8 + 2, KY(K) * 8 - 1), C1
LINE (KX(K) * 8 + 3, 83)-(KX(K) * 8 + 3, KY(K) * 8 - 1), C1
FOR SS = 1000 TO 1200 STEP 50
SOUND SS, .1
NEXT SS
NEXT SND
LINE (KX(K) * 8 + 2, 83)-(KX(K) * 8 + 2, KY(K) * 8 - 1), 0
LINE (KX(K) * 8 + 3, 83)-(KX(K) * 8 + 3, KY(K) * 8 - 1), 0
CALL STARSSUB
IF KD >= KS THEN
 KHIT = FF: CALL KDESTROYED
END IF

'DAMAGE INFLICTED

PSET (KX(FF) * 8, KY(FF) * 8 - 8), 0
 DRAW "s4r3d1r2u1r2d1r1d2l1d2r1d1l1d1l3u1l2d1l2u1l1u2r1u2l1u1r1u1bf3p0,0"
CALL SHIPSSUB
IF KD < KS THEN
 LOCATE 12, 2: PRINT SPACE$(22)
 LOCATE 12, 2: COLOR 7: PRINT "DAMAGE:"; : COLOR 15: PRINT STR$(DD)
FOR TM = 1 TO 6000: NEXT
END IF
NEXTFIRING:
NEXT FF
NEXTTRN = 1
PHASEREND:
END SUB

SUB RAND

FOR A = 1 TO 3
 RN(A) = 0
 RNDM(A) = 0
NEXT A
'LOCATE 1, 1: COLOR 7: PRINT RNDN
IF RNDN = 0 THEN GOTO ENDRAND
NEWRND:
H = 0: M = 0: L = RNDN + 1
FOR A = 1 TO 3
 RN(A) = INT(RND(1) * RNDN) + 1
NEXT A
FOR A = 1 TO 3
 IF RN(A) > H THEN H = RN(A)
 IF RN(A) < L THEN L = RN(A)
 IF RN(A) > L AND RN(A) < H THEN M = RN(A)
NEXT A
IF M = 0 AND RNDN > 2 THEN GOTO NEWRND
IF M = 0 THEN M = H

RNDM(1) = L: RNDM(2) = M: RNDM(3) = H
'LOCATE 1, 1: PRINT RNDM(1); RNDM(2); RNDM(3)
'SLEEP 2

ENDRAND:
END SUB

SUB REGISTRATION


CLS
COLOR 13
PRINT "REGISTRATION"
PRINT
PRINT "THIS IS A DEMO VERSION OF STARSHIP. STARSHIP IS A FORWARD VIEW STARSHIP"
PRINT "BATTLE SIMULATOR. THE REGISTERED VERSION INCLUDES EXTRA GRAPHICS (BLACK HOLES, "
PRINT "GALAXIES, AND PLANETS), LEVELS 2-4, 3 MORE ENEMY WARSHIPS, (DESTROYERS,"
PRINT "CRUISERS, AND BATTLESHIPS), TECH SUPPORT, AND UPGRADES. IM ONLY ASKING"
PRINT "FOR $5 FOR THIS GAME. I CAN BE CONTACTED ON AOL AT CHRISD7538. ALL "
PRINT "REGISTRATION FEES CAN BE SENT TO"
PRINT
PRINT "5325 CURRY FORD RD. #H-101 ORLANDO FL."
PRINT
PRINT "**PLEASE SEND CASH ONLY**"
PRINT
PRINT "WITH YOUR REGISTRATION PLEASE SEND YOUR ONLINE NAME WITH AOL AND I WILL"
PRINT "E-MAIL YOU WHEN I RECEIVE YOUR PAYMENT AND THEN I WILL MAIL YOUR"
PRINT "FULL COPY OF THIS GAME. I WILL BE ADDING A LOT MORE TO THIS GAME IN FUTURE"
PRINT "VERSIONS. I WILL CONTACT REGISTERED USERS VIA E-MAIL WHEN THESE BECOME"
PRINT "AVAILABLE."
PRINT
PRINT "FUTURE VERSIONS WILL INCLUDE,"
PRINT ">CUSTOM BATTLES"
PRINT ">MORE VARIETY OF ENEMY SHIPS"
PRINT ">THE ABILITY TO FIGHT ON DIFFERENT SHIPS"
PRINT ">AND MUCH MORE"
PRINT "PLEASE E-MAIL ANY SUGGESTIONS YOU MAY HAVE."
PRINT
PRINT "HIT ANY KEY"
DO
A$ = INKEY$
LOOP WHILE A$ = ""

END SUB

SUB SHEILDSSUB

IF SHEILDS <= 0 AND SHLDS$ = "Y" THEN GOTO S2
IF SHEILDSC = 0 THEN GOTO SHEILDPRINT
SHEILDSC = 0
NEXTTRN = 0
S2:
COLOR 15
LOCATE 12, 2: PRINT SPACE$(22)
IF SHLDS$ = "N" THEN
 LOCATE 12, 2: PRINT "RAISING SHEILDS"
 SHLDS$ = "Y"
 SA = 100: SB = 300
 FOR SND = 1 TO 100
 FOR SS = SA TO SB STEP 100
 SOUND SS, .05
 NEXT SS
 SA = SA + 2: SB = SB + 2
 NEXT SND
ELSEIF SHLDS$ = "Y" THEN
 LOCATE 12, 2: PRINT "LOWERING SHEILDS"
 SHLDS$ = "N"
 SA = 300: SB = 500
 FOR SND = 1 TO 100
 FOR SS = SB TO SA STEP -100
 SOUND SS, .05
 NEXT SS
 SA = SA - 2: SB = SB - 2
 NEXT SND
END IF
SHEILDPRINT:
IF SHLDS$ = "N" THEN
 SC = 7
ELSEIF SHEILDS > 500 AND SHLDS$ = "Y" THEN
 SC = 2
ELSEIF SHEILDS <= 250 AND SHLDS$ = "Y" THEN
 SC = 4
ELSEIF SHEILDS <= 500 AND SHLDS$ = "Y" THEN
 SC = 14
END IF
IF SC <> POINT(4, 4) THEN
 COLOR SC
 FOR Y = 1 TO 25
 FOR X = 1 TO 40
 IF BRDR(Y, X) <> 0 THEN
  LOCATE Y, X: PRINT CHR$(BRDR(Y, X))
 END IF
 NEXT X: NEXT Y
END IF
ENDSHEILDS:
END SUB

SUB SHIPDESTROYED
CLS
COLOR 14
LOCATE 12, 14
PRINT "SHIP DESTROYED!"
 FOR Z = 1 TO 1500
  SND = INT(RND(1) * 500) + 37
  SOUND SND, .025
 NEXT Z
FOR TM = 1 TO 10000: NEXT
CLS
COLOR 14
PRINT "THE FEDERATION STARBASES WERE DESTROYED!"
FOR TM = 1 TO 10000: NEXT: PRINT
COLOR 15
PRINT "OMEGANS DESTROYED:"; KDSTRYD
FOR TM = 1 TO 10000: NEXT
PRINT
PRINT "SCORE:"; STR$(CINT(SCORE * .75))
PRINT
FOR TM = 1 TO 10000: NEXT
PRINT "RATING:"; STR$(CINT(SCORE / TOTAL * 75)); "%"
FOR TM = 1 TO 10000: NEXT
PRINT
PRINT "PLAY AGAIN?"
PLAYAGN:
DO
A$ = INKEY$
LOOP WHILE A$ = ""
IF A$ = "Y" THEN RUN
IF A$ = "N" THEN END
GOTO PLAYAGN
END SUB

SUB SHIPSSUB
FOR A = 1 TO 3
 IF KY(A) > 0 THEN
  LOCATE KY(A), KX(A): COLOR KCLR(KN(A))
  SH = KSHLD(KN(A)): DM = KDMG(KN(A))
  CLR = 2
  IF DM >= SH * .5 THEN CLR = 14
  IF DM >= SH * .75 THEN CLR = 4
  KK = KCLR(KN(A)): KKC = KK + 8
  IF KK = 6 THEN KK = 6: KKC = 42
  LOCATE KY(A), KX(A): PRINT "   "
  KXX = KX(A) * 8: KYY = KY(A) * 8
  IF EVADING(A) = 0 THEN
   'FORWARD
    PSET (KXX + 1, KYY - 7), KKC
    DRAW "S4 R3D1R2L7R2D2L1D1R1D1R3U1R1U1L1U1L2D1R1D1L1"
    DRAW "C4 R1"
    DRAW "C15 BU2R1L3"
    PSET (KXX + 5, KYY - 5), KK
    DRAW "R3D1L2D1R1U1R6F1D1G1 BL21 H1U1E1R6D1R1U2R1L3"
    PSET (KXX - 1, KYY - 7), KK
    PSET (KXX + 6, KYY - 7), KK
   ELSE
   'BEHIND
    PSET (KXX + 1, KYY - 7), KKC
    DRAW "S4 R3BD5L3"
    PSET (KXX - 1, KYY - 7), KK
    DRAW "D1R7U1D2L2R4D1L1R6F1D1G1 BL21 H1U1E1R5U1R1D1U1R3D1R3D1R3L9"
    PSET (KXX + 2, KYY - 5), 4: DRAW "R1"
    PSET (KXX - 1, KYY - 4), 14: DRAW "R1BR5R1"
   END IF
  COLOR 9
  LOCATE KY(A) + 1, KX(A) - 1: PRINT CHR$(A + 48); SPC(3);
  IF KR(A) = 3 THEN COLOR 4
  IF KR(A) = 2 THEN COLOR 14
  IF KR(A) = 1 THEN COLOR 2
  PRINT RANGE$(KR(A))
  LOCATE KY(A) - 1, KX(A): PRINT "    "
 COLOR CLR
 JJ = SH - DM
 IF JJ < 100 AND JJ >= 10 THEN LOCATE KY(A) - 1, KX(A)
 IF JJ > 99 AND JJ < 1000 THEN LOCATE KY(A) - 1, KX(A) - 1
 IF JJ < 10 THEN LOCATE KY(A) - 1, KX(A)
 IF JJ = 1000 THEN LOCATE KY(A) - 1, KX(A) - 1
 END IF
 IF KX(A) > 0 THEN PRINT STR$(JJ)
NEXT A
END SUB

SUB SREPAIRS
IF DAMAGE = 0 THEN GOTO ENDRPRS
V = INT(RND(1) * 100) + 1
IF V <= 20 THEN
 RNDN = 20: CALL RAND: R = RNDM(3)
 IF DAMAGE - R < 0 THEN R = DAMAGE
 DAMAGE = DAMAGE - R
 LOCATE 12, 2: PRINT SPACE$(22)
 IF R > 0 THEN COLOR 15: LOCATE 12, 2: PRINT "SHIP REPAIRS:"; STR$(R); "%"
 FOR TM = 1 TO 6000: NEXT
END IF

J99 = INT((DAMAGE - 10) / 15)
IF J99 < 0 THEN J99 = 0
IF SYSDMG > J99 THEN
 DO
  SYSDMG = SYSDMG - 1
RPR:
  B = INT(RND(1) * 5) + 1
  IF B = 1 AND PDMG = 0 THEN GOTO RPR
  IF B = 2 AND SDMG = 0 THEN GOTO RPR
  IF B = 3 AND WDMG = 0 THEN GOTO RPR
  IF B = 4 AND TDMG = 0 THEN GOTO RPR
  IF B = 5 AND IDMG = 0 THEN GOTO RPR
  IF B = 1 THEN PDMG = 0: A$ = "PHASERS"
  IF B = 2 THEN SDMG = 0: A$ = "SHEILDS"
  IF B = 3 THEN WDMG = 0: A$ = "WARP DRIVE"
  IF B = 4 THEN TDMG = 0: A$ = "TORPEDOES"
  IF B = 5 THEN IDMG = 0: A$ = "IMPULSE"
  LOCATE 12, 2: PRINT SPACE$(22)
  COLOR 15
  LOCATE 12, 2: PRINT A$; " REPAIRED!"
  FOR TM = 1 TO 6000: NEXT
 LOOP WHILE SYSDMG > INT((DAMAGE - 10) / 15)
END IF
ENDRPRS:
END SUB

SUB STARSSUB
FOR A = 1 TO 100
 IF STX(A) < 1 THEN
XYLOC:
  Y = INT(RND(1) * 72) + 8: X = INT(RND(1) * 304) + 8
  STX(A) = X: STY(A) = Y
  STC(A) = INT(RND(1) * 15) + 17
 END IF
 NO = 0
 SX = STX(A): SY = STY(A)
 IF POINT(SX, SY) = STC(A) THEN GOTO NXTSTR
 IF POINT(SX, SY) <> 0 AND POINT(SX, SY) <> STC(A) THEN NO = 1
 FOR Z = 1 TO 3
  KX = KX(Z): KY = KY(Z)
  IF KX > 0 THEN
   XX1 = ABS(SX - (KX * 8 + 4)): YY1 = ABS(SY - (KY * 8 - 4))
   IF XX1 < 24 AND YY1 < 16 THEN NO = 1
  END IF
 NEXT Z
 IF NO = 0 THEN PSET (SX, SY), STC(A)
NXTSTR:
NEXT A
END SUB

SUB TEST
CLS : COLOR 15
LOCATE 1, 1: COLOR 15: PRINT SECTOR(SECTORX, SECTORY)
A$ = INKEY$
DO
A$ = INKEY$
LOOP UNTIL A$ <> ""
CLS
CALL DISPLAY
CALL STARSSUB
CALL SHIPSSUB
END SUB

SUB TORPEDOESSUB

TRPN = TORPEDOES
IF TRPN > 2 THEN TRPN = 2
IF TDMG = 1 THEN TRPN = 1
IF TRPN > INT(ENERGY / 100) THEN TRPN = INT(ENERGY / 100)
IF TRPN = 0 THEN
 LOCATE 12, 2: PRINT SPACE$(22)
 LOCATE 12, 2: COLOR 15
 PRINT "NOT ENOUGH ENERGY!"
FOR TM = 1 TO 4000: NEXT
 GOTO ENDTORPEDO
END IF
FOR TL = 1 TO TRPN
 TORPEDO(TL) = 0
 LOCATE 12, 2: PRINT SPACE$(22)
 LOCATE 12, 2: COLOR 7: PRINT "TORPEDO "; CHR$(48 + TL); ":   ": COLOR 15
 LOCATE 12, 13
TTENTER:
 DO
  A$ = INKEY$
 IF A$ = CHR$(8) THEN GOTO ENDTORPEDO
 LOOP WHILE A$ < "0" OR A$ > "3"
 IF KX(VAL(A$)) = 0 AND A$ <> "0" THEN GOTO TTENTER
 TORPEDO(TL) = VAL(A$)
 PRINT A$
FOR TM = 1 TO 4000: NEXT
NEXT TL
LOCATE 12, 2: PRINT SPACE$(22)
FOR T9 = 1 TO TRPN: IF TORPEDO(T9) > 0 THEN ENERGY = ENERGY - 100
NEXT T9
LOCATE 12, 2: COLOR 15: PRINT "FIRING TORPEDOES!"
FOR TT = 1 TO TRPN
FOR TM = 1 TO 4000: NEXT
 IF TORPEDO(TT) = 0 THEN GOTO NXTTORP
 TORPEDOES = TORPEDOES - 1
 COLOR 7
 LOCATE 12, 2: PRINT SPACE$(22)
 LOCATE 12, 2: PRINT "TORPEDO "; CHR$(48 + TT); ": ";
 COLOR 15
 TX = KX(TORPEDO(TT)) + 1: TY = KY(TORPEDO(TT))
 IF TY = 0 THEN HIT = 0: GOTO TMISS
 AA = 2000: BB = 37

 FOR SND = 1 TO 100
  FOR SS = AA TO BB STEP -100
   SOUND SS, .05
  NEXT SS
  AA = AA - 100
 NEXT SND

 FG = 0
 FOR TF = 78 TO TY * 8 - 1 STEP -4
  IF FG = 0 THEN FH = 42 ELSE FH = 43
  FG = FG + 1: IF FG > 1 THEN FG = 0
  PSET (TX * 8 - 6, TF), 4
  DRAW "S4R1D2L1U1L1R3"
  PSET (TX * 8 - 8, TF - 1), FH
  DRAW "S4 F1 BR3E1G1 BD2F1H1 BL3G1"
  FOR TM = 1 TO 200: NEXT
  PSET (TX * 8 - 6, TF), 0
  DRAW "S4R1D2L1U1L1R3"
  PSET (TX * 8 - 8, TF - 1), 0
  DRAW "S4 F1 BR3E1G1 BD2F1H1 BL3G1"
 NEXT TF

 CALL STARSSUB
 HIT = 105 - (KR(TORPEDO(TT)) * 10)
TMISS:
 V = INT(RND(1) * 100) + 1
 IF V > HIT THEN
  LOCATE 12, 13: COLOR 15: PRINT "MISS"
 FOR TM = 1 TO 4000: NEXT
 ELSE
  RNDN = 500: CALL RAND: DM = RNDM(3)
  KDMG(KN(TORPEDO(TT))) = KDMG(KN(TORPEDO(TT))) + DM
  KD = KDMG(KN(TORPEDO(TT)))
  KS = KSHLD(KN(TORPEDO(TT)))
  KM = 0
  KJ = INT(RND(1) * 100) + 1
  IF KD >= KS * .5 THEN KM = 2
  IF KD >= KS * .75 THEN KM = 3

  IF KDMG(KN(TORPEDO(TT))) >= KS THEN
   KHIT = TORPEDO(TT)
   CALL KDESTROYED
   GOTO NXTTORP
  END IF
  IF KJ >= 100 - (KM * 25) THEN
   LOCATE 12, 13: COLOR 15: PRINT "HIT!"
   PSET (TX * 8 - 9, TY * 8 - 8), 14
   DRAW "s4r3d1r2u1r2d1r1d2l1d2r1d1l1d1l3u1l2d1l2u1l1u2r1u2l1u1r1u1bf3p14,14"
   OX = TX * 8 - 10: OY = TY * 8 - 8
   FOR OZ = 1 TO 20
P105:
   OOX = INT(RND(1) * 11): OOY = INT(RND(1) * 9)
   OOX = OOX + OX: OOY = OOY + OY
   IF POINT(OOX, OOY) <> 14 THEN GOTO P105
   PSET (OOX, OOY), 8
   NEXT OZ
  ELSE
   PSET (TX * 8 - 9, TY * 8 - 8), 48
   DRAW "s4r3d1r2u1r2d1r1d2l1d2r1d1l1d1l3u1l2d1l2u1l1u2r1u2l1u1r1u1bf3p48,48"
  END IF

   FOR ZZ = 1 TO 500
    SND = INT(RND(1) * 250) + 37
    SOUND SND, .025
   NEXT ZZ
  FOR TM = 1 TO 4000: NEXT
   LOCATE TY, TX: PRINT "   "
   CALL SHIPSSUB
   LOCATE 12, 2: PRINT SPACE$(22)
   LOCATE 12, 2: COLOR 7: PRINT "DAMAGE:"; : COLOR 15: PRINT DM
  FOR TM = 1 TO 4000: NEXT
 END IF
NXTTORP:
NEXT TT
NEXTTRN = 1
ENDTORPEDO:
END SUB

SUB WINGAME
CLS
COLOR 14
IF OMEGANS = KDSTRYD THEN
 PRINT "THE OMEGAN STARSHIPS HAVE BEEN"
 PRINT "DESTROYED!"
 GOTO SCOREPRINT
END IF
IF STRDT <= 0 THEN
 PRINT "THE FEDERATION STARBASES WERE"
 PRINT "DESTROYED!"
END IF
SCOREPRINT:
COLOR 15
FOR TM = 1 TO 10000: NEXT
PRINT : PRINT "SCORE:"; SCORE
FOR TM = 1 TO 10000: NEXT
PRINT : PRINT "RATING:"; STR$(CINT(SCORE / TOTAL * 100)); "%"
FOR TM = 1 TO 10000: NEXT
PRINT : PRINT "PLAY AGAIN?"
PLAYAGN1:
DO
A$ = INKEY$
LOOP WHILE A$ = ""
IF A$ = "Y" THEN RUN
IF A$ = "N" THEN END
GOTO PLAYAGN1

END SUB

